plugins {
    id 'java'
    id 'checkstyle'
}

repositories {
    mavenCentral()
    maven {
        url "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
    }
}

dependencies {
    compile 'com.amazonaws:aws-lambda-java-core:1.2.1'
    compile 'com.amazonaws:aws-java-sdk-dynamodb:1.11.670'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'com.amazonaws:DynamoDBLocal:1.13.5'

}

task copyNativeDeps(type: Copy) {
    from (configurations.testCompile) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/libs'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/libs'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
checkstyle {
    configFile file('config/checkstyle/checkstyle.xml')
    toolVersion '8.21'
}
checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

jar {
    manifest {
    }
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}